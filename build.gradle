plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'maven-publish'
}

group = 'me.z7087.final2constant'
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.ow2.asm:asm:9.8'
}

JavaVersion JAVA_COMPATIBILITY = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    if (JAVA_COMPATIBILITY <= JavaVersion.VERSION_1_8) {
        // suppressed "source/target value 8 is obsolete and will be removed in a future release"
        options.compilerArgs << '-Xlint:-options'
    }
}

java {
    sourceCompatibility = JAVA_COMPATIBILITY
    targetCompatibility = JAVA_COMPATIBILITY

    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    //withSourcesJar()
}

tasks.shadowJar {
    mustRunAfter jar
    archiveClassifier = ''
    minimize()
    relocate("org.objectweb.asm", "me.z7087.final2constant.libs.org.objectweb.asm")
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
        }
    }
}